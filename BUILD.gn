# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  dep_base = rebase_path("./", "//")
} else {
  dep_base = rebase_path("../..", "//")
}

config("external_config") {
  include_dirs = [ "include" ]
}

config("internal_config") {
  include_dirs = [ "src" ]
}

executable("test") {
  sources = [
    "test.cc"
  ]

  include_dirs = [ "src" ]

  deps = [
    ":usermodel",
  ]
}

group("bat-native-usermodel") {
  public_deps = [
    ":usermodel",
  ]

  data_deps = [
    ":bat-native-usermodel-standalone",
  ]
}

static_library("bat-native-usermodel-standalone") {
  complete_static_lib = true

  output_dir = root_out_dir

  configs -= [ "//build/config/compiler:thin_archive" ]

  defines = [
    "STANDALONE_BUILD",
  ]

  deps = [
    ":usermodel"
  ]
}

static_library("usermodel") {

  public_configs = [ ":external_config" ]
  configs += [ ":internal_config" ]

  output_name = "bat_native_usermodel"

  sources = [
    "include/user_profile.h",
    "src/classifier.h",
    "src/bag_of_words_extractor.h",
    "src/bag_of_words_extractor.cc",
    "src/logistic_regression.h",
    "src/logistic_regression.cc",
    "src/naive_bayes.h",
    "src/naive_bayes.cc",
    "src/stmr.c",
    "src/stmr.h",
    "src/ads_ranker.h",
    "src/user_model.h",
    "src/user_model.cc",
    "src/user_profile.h",
    "src/user_profile.cc",
    "src/notification_event_type.h"
  ]

  deps = [
    rebase_path("bat-native-rapidjson", dep_base),
  ]
}
