# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/rules.gni")
}
if (defined(is_docker)) {
  dep_base = rebase_path("./", "//")
} else {
  dep_base = rebase_path("../..", "//")
}

config("external_config") {
  include_dirs = [ "include" ]
}

config("internal_config") {
  include_dirs = [ "src" ]
}

executable("test") {

  sources = [
    "test.cc"
  ]

  include_dirs = [ "src" ]

  deps = [
    ":usermodel",
    rebase_path("bat-native-rapidjson", dep_base),
  ]
}

group("bat-native-usermodel") {
  public_deps = [
    ":usermodel",
  ]

  data_deps = [
    ":bat-native-usermodel-standalone",
  ]
}

static_library("bat-native-usermodel-standalone") {
  complete_static_lib = true

  output_dir = root_out_dir

  configs -= [ "//build/config/compiler:thin_archive" ]

  defines = [
    "STANDALONE_BUILD",
  ]

  deps = [
    ":usermodel",
  ]
}

source_set("usermodel") {
  public_configs = [ ":external_config" ]
  configs += [ ":internal_config" ]

  output_name = "bat_native_usermodel"

  sources = [
    "include/bat/usermodel/ad.h",
    "include/bat/usermodel/ad_catalog.h",
    "include/bat/usermodel/notification_event_type.h",
    "include/bat/usermodel/usermodel_callback_handler.h",
    "include/bat/usermodel/user_profile.h",
    "include/bat/usermodel/user_model.h",
    "src/ad.cc",
    "src/ad_catalog.cc",
    "src/bag_of_words_extractor.h",
    "src/bag_of_words_extractor.cc",
    "src/naive_bayes.h",
    "src/naive_bayes.cc",
    "src/stmr.c",
    "src/stmr.h",
    "src/user_model_impl.h",
    "src/user_model_impl.cc",
    "src/user_profile.h",
    "src/user_profile.cc"
  ]

  deps = [
    "//base",
    rebase_path("bat-native-rapidjson", dep_base),
  ]
}
